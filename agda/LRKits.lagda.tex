\begin{code}
{-# OPTIONS --safe --without-K --postfix-projections --prop #-}
open import Algebra.Po
open import Level

module LRKits (poSemiring : PoSemiring 0ℓ 0ℓ 0ℓ) where

  open PoSemiring poSemiring renaming (Carrier to Ann)

  open import Algebra.Relational renaming (_↘_↙_ to _↘-_-↙_)
  open import Data.LTree
  open import Data.LTree.Vector
  open import Data.Product
  open import Data.Wrap
  open import Function using (id; _∘_; Equivalence)
  open import Relation.Binary.PropositionalEquality as ≡ using (_≡_)
  open import Relation.Nary.Extra using (_⊆_)
  open import Relation.Unary.Bunched

  data Ty : Set where
    ι : Ty
    `! : (r : Ann) (A : Ty) → Ty

  open import Generic.Linear.Operations rawPoSemiring
  open import Generic.Linear.Algebra poSemiring
  open import Generic.Linear.Syntax Ty Ann
  open import Generic.Linear.Syntax.Interpretation Ty rawPoSemiring hiding (◇)
  open import Generic.Linear.Variable Ty rawPoSemiring hiding (_↙ᵛ_; _↘ᵛ_)
  open import Generic.Linear.Environment Ty poSemiring
  open import Generic.Linear.Environment.Properties Ty poSemiring
  open import Generic.Linear.Environment.Categorical Ty poSemiring
\end{code}

%<*Tm>
\begin{code}
  Bind : Ctx → (Ctx → Set) → (Ctx → Set)
  Bind Δ T Γ = T (Γ ++ᶜ Δ)

  data _⊢_ : Ctx → Ty → Set where
    var : _∋_ ⊆ _⊢_
    !-I : ∀ {r A} → r · (_⊢ A) ⊆ _⊢ `! r A
    !-E : ∀ {r A C} → (_⊢ `! r A) ✴ Bind [ r ∙ A ]ᶜ (_⊢ C) ⊆ _⊢ C
\end{code}
%</Tm>

\begin{code}
  module _ {𝓥 : Ctx → Ty → Set} where

    env-+ : ∀ {s t γ δ P Qₗ Qᵣ} →
      let Γ = ctx {s} P γ; Δₗ = ctx {t} Qₗ δ; Δᵣ = ctx {t} Qᵣ δ in
      (λ Q → [ 𝓥 ] Γ ⇒ᵉ ctx Q δ) ◇ (_≤[ Qₗ +* Qᵣ ]) →
      (λ Pₗ → [ 𝓥 ] ctx Pₗ γ ⇒ᵉ Δₗ)
        ↘- P ≤[_+*_] -↙
      (λ Pᵣ → [ 𝓥 ] ctx Pᵣ γ ⇒ᵉ Δᵣ)
    env-+ (ρ , sp) =
      let fit-hereₗ ↘, sp′ ,↙ fit-hereᵣ = ρ .Ψ .rel-+ₘ (ρ .fit-here , sp) in
      record { [_]_⇒ᵉ_ ρ; fit-here = fit-hereₗ }
        ↘, sp′ ,↙
      record { [_]_⇒ᵉ_ ρ; fit-here = fit-hereᵣ }

    env-* : ∀ {s t γ δ P Q′ r} →
      let Γ = ctx {s} P γ; Δ′ = ctx {t} Q′ δ in
      (λ Q → [ 𝓥 ] Γ ⇒ᵉ ctx Q δ) ◇ _≤[ r *ₗ Q′ ] →
      P ≤[ r *ₗ_] ◇ (λ P′ → [ 𝓥 ] ctx P′ γ ⇒ᵉ Δ′)
    env-* (ρ , sp) =
      let fit-here′ , sp′ = ρ .Ψ .rel-*ₘ (ρ .fit-here , sp) in
      sp′ , record { [_]_⇒ᵉ_ ρ; fit-here = fit-here′ }
\end{code}

%<*Weakening>
\begin{code}
  Weakening : ∀ {v} {A : Set} → (Ctx → A → Set v) → Set v
  Weakening 𝓥 =
    ∀ {s t P} {γ : Vector Ty (s <+> t)} → P ∘ ↘ ≤* 0* →
    𝓥 (ctx (P ∘ ↙) (γ ∘ ↙)) ⊆ 𝓥 (ctx P γ)
\end{code}
%</Weakening>

%<*Kit>
\begin{code}
  record Kit (𝓥 : Ctx → Ty → Set) : Set where
    constructor kit
    field
      ↙ᵏ : Weakening 𝓥
      vr : _∋_ ⊆ 𝓥
      tm : 𝓥 ⊆ _⊢_
\end{code}
%</Kit>

\begin{code}
    open Equivalence
    private module Vᶠᴿ {s} = FRelLeftSemimodule (Vᶠᴿ s)
    open Vᶠᴿ
\end{code}

%<*bindEnv>
\begin{code}
    bindEnv : ∀ {Γ Δ Θ} → [ 𝓥 ] Γ ⇒ᵉ Δ → [ 𝓥 ] Γ ++ᶜ Θ ⇒ᵉ Δ ++ᶜ Θ
\end{code}
%</bindEnv>
\begin{code}
    bindEnv ρ .Ψ = [  [ ρ .Ψ  │ 0ᴿ ]ᴿ
                              ─
                      [ 0ᴿ    │ 1ᴿ ]ᴿ ]ᴿ
    bindEnv ρ .fit-here = _↘,_,↙_
      {left = _ ++ _} {right = _ ++ _}
      (ρ .fit-here , 0*-triv)
      (+*-identity↘ _ ++ₙ +*-identity↙ _)
      (0*-triv , ≤*-refl)
    bindEnv ρ .lookup ((ul , ur) ↘, sp+ ,↙ (dl , dr)) (lvar i q b)
      with un++ₙ sp+ | un++ₙ b
    bindEnv ρ .lookup ((ul , ur) ↘, sp+ ,↙ (dl , dr)) (lvar (↙ i) q b)
      | sp+l , sp+r | bl , br = ↙ᵏ
      (≤*-trans (+ₘ-identityˡ→ (ur , sp+r)) (≤*-trans dr br))
      (ρ .lookup (ρ .Ψ .rel-≤ₘ (+ₘ-identityʳ→ (sp+l , dl)) ≤*-refl ul)
        (lvar i q bl))
    bindEnv ρ .lookup ((ul , ur) ↘, sp+ ,↙ (dl , dr)) (lvar (↘ i) q b)
      | sp+l , sp+r | bl , br = vr (lvar (↘ i) q
      (≤*-trans (+ₘ-identityʳ→ (sp+l , dl))
        (0*→≤* (ρ .Ψ .rel-0ₘ (ul , ≤*→0* bl)))
      ++ₙ
      ≤*-trans (+ₘ-identityˡ→ (ur , sp+r)) (≤*-trans dr br)))
\end{code}

%<*trav>
\begin{code}
    trav : ∀ {Γ Δ A} → [ 𝓥 ] Γ ⇒ᵉ Δ → Δ ⊢ A → Γ ⊢ A
    trav ρ (var x) = tm (ρ .lookup (ρ .fit-here) x)
    trav ρ (!-I (⟨ sp* ⟩·ᶜ M)) =
      let sp*′ , ρ′ = env-* (ρ , sp*) in
      !-I (⟨ sp*′ ⟩·ᶜ (trav ρ′ M))
    trav ρ (!-E (M ✴ᶜ⟨ sp+ ⟩ N)) =
      let ρₗ ↘, sp+′ ,↙ ρᵣ = env-+ (ρ , sp+) in
      !-E (trav ρₗ M ✴ᶜ⟨ sp+′ ⟩ trav (bindEnv ρᵣ) N)
\end{code}
%</trav>

%<*dlv>
\begin{code}
  ↙ᵛ : Weakening _∋_
  ↙ᵛ R0 v .idx = ↙ (v .idx)
  ↙ᵛ R0 v .tyq = v .tyq
  ↙ᵛ R0 v .basis = v .basis ++ₙ R0
\end{code}
%</dlv>

\begin{code}
  open Kit
\end{code}

%<*var-kit>
\begin{code}
  ∋-kit : Kit _∋_
  ∋-kit = kit ↙ᵛ id var

  ren : ∀ {Γ Δ A} → [ _∋_ ] Γ ⇒ᵉ Δ → Δ ⊢ A → Γ ⊢ A
  ren = trav ∋-kit
\end{code}
%</var-kit>

\begin{code}
  instance
    identityEnv-⊢ : IdentityEnv _⊢_
    identityEnv-⊢ .pure = var
\end{code}

%<*dlv-env>
\begin{code}
  ↙^Env : {𝓥 : Ctx → Ty → Set} → Weakening 𝓥 → Weakening [ 𝓥 ]_⇒ᵉ_
  ↙^Env ↙^𝓥 R0 ρ .Ψ = [ ρ .Ψ │ 0ᴿ ]ᴿ
  ↙^Env ↙^𝓥 R0 ρ .fit-here = ρ .fit-here , ≤*→0* R0
  ↙^Env ↙^𝓥 R0 ρ .lookup (r , sp0) v = ↙^𝓥 (0*→≤* sp0) (ρ .lookup r v)

  ↙ᵛ-env :
    ∀ {s t P} {γ : Vector Ty (s <+> t)} → P ∘ ↘ ≤* 0* →
    [ _∋_ ] ctx P γ ⇒ᵉ ctx (P ∘ ↙) (γ ∘ ↙)
  ↙ᵛ-env R0 = ↙^Env ↙ᵛ R0 id^Env
\end{code}
%</dlv-env>

%<*sub>
\begin{code}
  ⊢-kit : Kit _⊢_
  ⊢-kit = kit (λ R0 → ren (↙ᵛ-env R0)) var id

  sub : ∀ {Γ Δ A} → [ _⊢_ ] Γ ⇒ᵉ Δ → Δ ⊢ A → Γ ⊢ A
  sub = trav ⊢-kit
\end{code}
%</sub>

\begin{code}
  idˢ : ∀ {Γ} → [ _⊢_ ] Γ ⇒ᵉ Γ
  idˢ = id^Env

  psh-⊢ : IsPresheaf _⊢_
  psh-⊢ QP = ren (subuse^Env QP)

  mutual
    open With-psh^𝓥 psh-⊢
\end{code}

%<*subSingle>
\begin{code}
    sub[-] : ∀ {s P Q R γ r A B} → R ≤[ Q +*_] ◇ _≤[ r *ₗ P ] →
      ctx {s} P γ ⊢ A → ctx Q γ ++ᶜ [ r ∙ A ]ᶜ ⊢ B → ctx R γ ⊢ B
    sub[-] {Q = Q} {R} {γ} {r} {A} (sp+ , sp*) t = sub σ
      where
      σ : [ _⊢_ ] ctx R γ ⇒ᵉ ctx Q γ ++ᶜ [ r ∙ A ]ᶜ
      σ = ++ᵉ (id^Env ✴ᶜ⟨ sp+ ⟩ [-]ᵉ (⟨ sp* ⟩·ᶜ t))
\end{code}
%</subSingle>
